#class liandu open
#alias {ld.start} {
	#var ld_count 0;
	show_to_quest 炼毒: ${ld_count};
	#class ld.inner open;

	
	#action {一阵风吹过，把枯乾的骸骨化成骨灰吹散了} {
		#unticker {give_corpse};
		ask piaoran about 炼毒;		
	};
	
	#action {你将自身精力内力灌注入星宿派%*的尸体} {
		#unaction {星宿派%*死了};
		#delay {3} {
			get corpse;
		};
	};

	#action {炼毒时需要双手的配合，所以你最好还是不要拿武器} {
		unwi;
		liandu;
	};
	
	#action {你刚刚炼毒完成不久，频繁炼毒会有生命危险的！} {
		#delay {ld} {
			unwi;
			liandu;
		} {30};
	};

	#action {你身上没有 corpse 这样东西。} {
		#unticker {give_corpse};
		ask piaoran about 炼毒;
	};
	
	#action {你将%*尸体扶了起来背在背上} {
		#delay {1} {
			map.go 石棺;
			#ticker {give_corpse} {
				give piaoran corpse;	
			} {10};
			on_unbusy {
				give piaoran corpse;	
			};
		};
	};

	#action {这一瞬间，你发觉那至阴的毒气和自己体内原本的毒气相互融合，一同归纳进了气海！} {
		#math ld_count ${ld_count}+1;
		show_to_quest 炼毒: ${ld_count};
		#delay {3} {
			chifan;
			dz.full {
				tn.full {
					ask piaoran about 炼毒;
				};
			};
		};
	};
	
	#action {飘然子对着你嘿嘿一笑：“想练毒是吧？先去给你自己找具有用的尸体来} {
		ld.get_corpse;
	};
	              
	#action {飘然子说道：「好吧，你来试着练习(liandu)一下毒技吧。」} {
		#unticker {give_corpse};
		unwi;
		liandu;
	};

	#class ld.inner close;
	load_map;
	map.go 石棺;
	dz.full {
		tn.full {
			ask piaoran about 炼毒;
		};
	};
};

#alias {ld.stop} {
	#class ld.innder kill;
	show_to_quest 炼毒结束;
};

#alias {ld.get_corpse} {
	drop tonghao;
	drop tonggu;
	drop tongbo;
	wi;
	out;
	on_truce {
		map.go 巨岩;
		#delay {3} {
			kill_somebody;
			#action {星宿派%*死了} {
				zhishi corpse;				
			} {1};
		};
	};
};

#alias {kill_somebody} {
	#class kill_somebody.inner open;
	#action {星宿派鼓手} {
		#class kill_somebody.inner kill;
		kill gushou;
	};
	#action {星宿派号手} {
		#class kill_somebody.inner kill;
		kill haoshou;
	};
	#action {星宿派钹手} {
		#class kill_somebody.inner kill;
		kill boshou;
	};
	#ticker {l} {
		l;
	} {60};
	#class kill_somebody.inner close;
	l;
};
#class liandu close