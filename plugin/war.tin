#class war open;

#alias {re} {
    yun recover;
	yun regenerate;
};

#var heal_count 1;
#alias {heal} {
	re;
	#var heal_count %1;
	#loop {1} {${heal_count}} {tmp} {
		yun heal;
		yun inspire;
	};
};

#alias {unwi} {
    unwield all;
};

#action {看起来%1想杀死你} {
	war;
};

#action{「%1」%2(%3) <战斗中>} {
	show_to_info %1:%2:%3;
}

#alias {qi} {
    yun qi;
};

#alias {equip} {
    down;
    out;
    remove all;
	#foreach {${my_weapons}[%*]} {weapon} {draw ${weapon}};
    draw armor;
    draw head;
    draw cloth;
    draw surcoat;
    draw boots;
    wear all;
	wi;
    enter; up;
};

#alias {play_music} {
    #system {bin/play %1};
};

#action {君子报仇十年不晚，咱们走着瞧} {
	#nop play_music win;
};

#action {你似乎十分疲惫，看来需要好好休息了} {re} {1};
#action {你气喘嘘嘘，看起来状况并不太好} {re} {1};
#action {你看起来已经力不从心了} {re} {1};

#list poisions create {
	你一中掌，身子一晃，看来已经中了毒！;
	你突然打了个寒战，明显感到一股寒流已经侵入体内！;
	忽然一阵刺骨的奇寒袭来;
	突然你打了个寒战，你中的冰魄寒毒发作了！;
	他的剧毒散至你的全身;
};
#foreach {${poisions}[%*]} {poision} {
	#action {${poision}} {
		qudu;
	} {1};
};

#alias {chan_a_chan} {
	chan;
	#ticker {chan_a_chan} {
		chan;
	} {1};
	#action {缠: miss} {
		halt;
	};
    #delay {2} {
		#unaction {缠: miss};
		#unticker {chan_a_chan};
	};
};

#alias {kill} {
	war;
	#send kill %0;
};

#alias {killall} {
	war;
	#send killall %0;
};

#alias {hit} {
	war;
	#send hit %0;
};

#alias {fight} {
	war;
	#send fight %0;
};

#alias {wi_normal} {
	unwi;
	#foreach {${my_weapons}[%*]} {weapon} {wield ${weapon}};
};

#alias {wi_bare} {
	unwi;
};

#alias {wi_xiaofeng} {
	wi_normal;
};

#alias {wi} {
	wi_normal;
};

#action {你似乎十分疲惫，看来需要好好休息了} {
	re;
};

#action {已经断为两截} {
	show_to_info 武器断了;
	wi;
};

#alias {detect_mingjiao} {
	l %0;
	#action {这是一位明教} {
		mj.start;
	};
	
	#action {圣火令} {
		mj.start;
	};
	
	#delay {2} {
		#unaction {圣火令};
		#unaction {这是一位明教};
	};
};

#var mj_started 0;
#alias {mj.start} {
	#if {${mj_started} == 0} {
		#var mj_started 1;
		#class war.inner kill;
		#class attack.inner kill;
		show_to_info >>明教;
		#alias {wi} {wi_bare};
		unwi;
		#alias {attack} {
			attack.mingjiao;
		};
		war;		
	};
};

#var mj_id {};
#alias {mj.stop} {
	#if {${mj_started} == 1} {
		#var mj_started 0;
		#var mj_id {};
		show_to_info <<明教;
		#alias {wi} {wi_normal};
		wi;
		#class attack.inner kill;
		#alias {attack} {
			attack.normal;
		};
	};
};

#alias {qudu} {
	eat san;
};

#alias {raw} {
	#class war.inner kill;
	#class attack.inner kill;
	jiali none;
	mj.stop;
	#showme truce;
};

#alias {war} {
	#class war.inner open;
	#action {你全身放松，坐下来开始运功疗伤。} {
		raw;
	};
	#ticker {fighting} {
		yun heal;
	} {5};
	#ticker {yun} {
		yun qi;
		yun jing;
	} {10};
	#action {wait_for_fight} {
		#delay {attack.delay} {
			attack;
		} {0.5};
	};
	#class war.inner close;
	yun qi;
	yun jing;
	attack.prepare;
	attack;
};

#alias {attack}  {
	attack.normal;
};

#alias {attack.hubiao} {
	attack.normal;
};

#action {乾坤大挪移自然激发而出，将你的攻势反击了回去。} {
	mj.start;	
};

#action {^%*运起九阳神功} {
	mj.start;
	#showme mingjiao enemy:%1;
};

#alias {tao} {
	#action {get_exits: done} {
		#unaction {get_exits: done};
		#list exits_list get 1 exit;
		on_unbusy {
			${exit};
		};
	};
	#5 halt;
	get_exits;
};

#action {${char_name}%*不由自主的放开了兵器} {
	wi;
};

#class war close;