#class wield open
#var current_weapons_set 0;

#alias {unwi} {
    unwield all;
};
#alias {wi} {
    unwi;
    #foreach {${weapons_to_wield}[%*]} {weapon} {
        do_wield ${weapon};
    };
};

#action {已经断为两截} {
    show_to_info 武器断了;
    wi;
};

#alias {ss} {
    #math current_weapons_set ${current_weapons_set}+1;
    #list my_weapons_to_wield_${current_weapons_set} size tmp_size;
    #if {${tmp_size} == 0} {
        #var current_weapons_set 0;
    };
    #if {${current_weapons_set} == 0} {
        #var weapons_to_wield ${my_weapons_to_wield};
    };
    #else {
        #var weapons_to_wield ${my_weapons_to_wield_${current_weapons_set}};
    };
    #show weapons_to_wield switched to ${weapons_to_wield};
    reset_weapons;
};

#var wielding 0;
#alias {do_wield} {
    #if {${wielding} == 1} {
        #delay {1.8} {
            do_wield %0;
        };
    };
    #else {
        #var target %0;
        #replace target { } {_};
        #var current_weapon ${weapon_${target}};
        #if {"${current_weapon}" == ""} {
            #var wielding 1;
            wield_1 %0;
        };
        #else {
            #class wield_inner open;
            #action {你身上没有这样东西。} {
                #class wield_inner kill;
                #var wielding 1;
                #var weapon_${target} {};
                wield_1 %%0;
            };
            #action {^>} {
                #class wield_inner kill;
            };
            #class wield_inner close;
            wield ${current_weapon};
            #showme weapon wield;
        };
    };
};

#gag weapon wield;
#alias {wield_1} {
    #var target %0;
    wield my ${target};
    #var weapon_name {my ${target}};
    #class wield_inner open;
    #action {你身上没有这样东西。} {
        #class wield_inner kill;
        #undelay {wield};
        #delay {0.1} {
            wield_2 ${target};
        };
    };
    #action {^>} {
        #class wield_inner kill;
        #replace target { } {_};
        #var weapon_${target} ${weapon_name};
        #showme weapon_${target} set to ${weapon_name};
        add_to_my_weapons ${weapon_name};
        #var wielding 0;
        #showme weapon wield;
    };
    #class wield_inner close;
};

#alias {wield_2} {
    #var target %0;
    wield ${target};
    #var weapon_name {${target}};
    #class wield_inner open;
    #action {你身上没有这样东西。} {
        #class wield_inner kill;
        #delay {0.1} {
            wield_3 ${target};
        };
    };
    #action {^>} {
        #class wield_inner kill;
        #replace target { } {_};
        #var weapon_${target} ${weapon_name};
        #showme weapon_${target} set to ${weapon_name};
        #var wielding 0;
        #showme weapon wield;
    };
    #class wield_inner close;
};

#alias {wield_3} {
    #var target %0;
    #var weapon_name special_${target};
    #replace weapon_name { } {_};
    #show ${weapon_name} -> ${${weapon_name}};
    wield ${${weapon_name}};
    #class wield_inner open;
    #action {你身上没有这样东西。} {
        #class wield_inner kill;
        #showme 没有可用的武器;
        #var wielding 0;
        #showme weapon wield;
    };
    #action {^>} {
        #class wield_inner kill;
        #replace target { } {_};
        #var weapon_${target} ${${weapon_name}};
        add_to_my_special_weapons ${${weapon_name}};
        #var wielding 0;
        #showme weapon wield;
    };
    #class wield_inner close;
};

#alias {reset_weapons} {
    #var weapons_index 0;
    #var weapons_size 0;
    #list weapons_to_wield size weapons_size;
    reset_one_weapon;
};

#alias {reset_one_weapon} {
    #math weapons_index ${weapons_index}+1;
    #if {${weapons_index} <= ${weapons_size}} {
        #list weapons_to_wield get ${weapons_index} tmp_weapon;
        wield_1 ${tmp_weapon};
    };
    #else {
        unwi;
        wi;
    }
    #action {weapon wield} {
        #unaction {weapon wield};
        reset_one_weapon;
    };
};

#list my_weapons create {};
#alias {add_to_my_weapons} {
    #var weapon_name %0;
    #list my_weapons find ${weapon_name} tmp;
    #if {${tmp} != 0} {
        #list my_weapons del ${tmp};
    };
    #list my_weapons add ${weapon_name};
};

#list my_special_weapons create {};
#alias {add_to_my_special_weapons} {
    #var weapon_name %0;
    #list my_special_weapons find ${weapon_name} tmp;
    #if {${tmp} != 0} {
        #list my_special_weapons del ${tmp};
    };
    #list my_special_weapons add ${weapon_name};
};

#class wield close