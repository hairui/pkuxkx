#class wield open
#var current_weapons_set 0;

#alias {unwi} {
    unwield all;
};
#alias {wi} {
    unwi;
    #foreach {${weapons_to_wield}[%*]} {weapon} {
        do_wield ${weapon};
    };
};

#action {已经断为两截} {
    show_to_info 武器断了;
    wi;
};

#alias {ss} {
    #math current_weapons_set ${current_weapons_set}+1;
    #list my_weapons_to_wield_${current_weapons_set} size tmp_size;
    #if {${tmp_size} == 0} {
        #var current_weapons_set 0;
    };
    #if {${current_weapons_set} == 0} {
        #var weapons_to_wield ${my_weapons_to_wield};
    };
    #else {
        #var weapons_to_wield ${my_weapons_to_wield_${current_weapons_set}};
    };
    #show 平砍武器切换为 ${weapons_to_wield};
    reset_weapons;
    wi;
};

#gag {你身上没有这样东西};
#var wielding 0;
#alias {do_wield} {
    #if {${wielding} == 1} {
        #delay {1} {
            do_wield %0;
        };
    };
    #else {
        #var target %0;
        #replace target { } {_};
        #var current_weapon ${weapon_${target}};
        #if {"${current_weapon}" == ""} {
            #var wielding 1;
            wield_1 %0;
        };
        #else {
            #class wield_inner open;
            #action {你身上没有这样东西。} {
                #class wield_inner kill;
                #var wielding 1;
                #var weapon_${target} {};
                wield_1 %%0;
            };
            #delay {wield_delay} {
                #class wield_inner kill;
            } {0.5};
            #class wield_inner close;
            wield ${current_weapon};
            #showme weapon wield;
        };
    };
};

#gag weapon wield;
#alias {wield_1} {
    #var target %0;
    #var weapon_name {my ${target}};
    #class wield_inner open;
    #action {你身上没有这样东西。} {
        #class wield_inner kill;
        #undelay {wield};
        #delay {0.1} {
            wield_2 ${target};
        };
    };
    #delay {wield_delay} {
        #class wield_inner kill;
        #replace target { } {_};

        #class weapons open;
        #var weapon_${target} ${weapon_name};
        #class weapons close;

        #showme ${target} 设置为 ${weapon_name};
        add_to_my_weapons ${weapon_name};
        #var wielding 0;
        #showme weapon wield;
    } {0.5};
    #class wield_inner close;
    wield my ${target};
};

#alias {wield_2} {
    #var target %0;
    #var weapon_name {${target}};
    #class wield_inner open;
    #action {你身上没有这样东西。} {
        #class wield_inner kill;
        #delay {0.1} {
            wield_3 ${target};
        };
    };
    #delay {wield_delay} {
        #class wield_inner kill;
        #replace target { } {_};
        
        #class weapons open;
        #var weapon_${target} ${weapon_name};
        #class weapons open;
        
        #showme ${target} 设置为 ${weapon_name};
        #var wielding 0;
        #showme weapon wield;
    } {0.5};
    #class wield_inner close;
    wield ${target};
};

#alias {wield_3} {
    #var target %0;
    #var weapon_name special_${target};
    #replace weapon_name { } {_};
    #show ${weapon_name} -> ${${weapon_name}};
    #class wield_inner open;
    #action {你身上没有这样东西。} {
        #class wield_inner kill;
        #showme 没有可用的武器;
        #var wielding 0;
        #showme weapon wield;
    };
    #delay {wield_delay} {
        #class wield_inner kill;
        #replace target { } {_};

        #class weapons open;
        #var weapon_${target} ${${weapon_name}};
        #class weapons close;

        #showme ${target} 设置为 ${weapon_name};
        add_to_my_special_weapons ${${weapon_name}};
        #var wielding 0;
        #showme weapon wield;
        
    } {0.5};
    #class wield_inner close;
    wield ${${weapon_name}};
};

#list my_weapons create {};
#alias {add_to_my_weapons} {
    #var weapon_name %0;
    #list my_weapons find ${weapon_name} tmp;
    #if {${tmp} != 0} {
        #list my_weapons del ${tmp};
    };
    #list my_weapons add ${weapon_name};
};

#list my_special_weapons create {};
#alias {add_to_my_special_weapons} {
    #var weapon_name %0;
    #list my_special_weapons find ${weapon_name} tmp;
    #if {${tmp} != 0} {
        #list my_special_weapons del ${tmp};
    };
    #list my_special_weapons add ${weapon_name};
};

#alias reset_weapons {
    #class weapons kill;
};

#action {你必须空出一只手来使用武器} {
    remove shield;
};
#class wield close