#class hubiao open
load hubiao_data;
load chaos;

#var hb_first_kill 0;
#var hb_wait 0;
#var hb_paused 0;
#var hb_hb_force_wait 0;
#var hb_paused 0;
#var hb_auto 0;
#var hb_info nil;
#var hb_rotate_location NIL;
#var hb_rotate_location_count 0;

#alias {hb.teardown} {
	#class hb_setup_inner kill;
	#class clear_npc.inner kill;
	#unticker {kill_npc};
	#alias ge {\east};
	#alias genter {\enter};
	#alias geu {\eastup};
	#alias gn {\north};
	#alias gne {\northeast};
	#alias gnu {\northup};
	#alias gnw {\northwest};
	#alias gout {\out};
	#alias gs {\south};
	#alias gse {\southeast};
	#alias gsu {\southup};
	#alias gsw {\southwest};
	#alias gw {\west};
	#alias gwu {\westup};
	#alias ged {\eastdown};
	#alias gnd {\northdown};
	#alias gwd {\westdown};
	#alias gsd {\southdown};
	#alias gd {\down};
	#alias gu {\up};
	#alias {attack} {
		attack.normal;
	};
	#unticker {hb_ticker};
};

#alias {hb.move} {
	#if {${qx_percent} < 50 || ${js_percent} < 50 || ${nl_percent} < 20} {
		#unticker {hb_walk};
		#delay {hb_move} {
			show_to_info 气血或精神过低,推车暂停;
			notify escort paused;
			on_truce {
				on_health {hb.move};
			};
		} {3};
	};
	#else {
		#if {${hb_path_index} > ${hb_path_size}} {
			#delay {hb_move} {
				hb.pause;
				show_to_info 到达;
				notify.alert escort arrived;
				xmpp.notify 到达, 没找到收货人;
			} {3};
		};
		#else {
			#if {${eff_qx_percent} < 95} {
				#2 yun heal;
			};
			#if {${eff_qx_percent} < 50} {
				#2 yun lifeheal;
			};
			#ticker {hb_walk} {
				#if {${hb_force_wait} == 0 && ${hb_paused} == 0 && ${hb_wait} == 0} {
					${hb_next_step};
				};
			} {8};
			#delay {hb_move} {
				#if {${hb_force_wait} == 0 && ${hb_paused} == 0 && ${hb_wait} == 0} {
					${hb_next_step};
				};
			} {3};
		};
	};
};

#alias {hb.setup} {
	#foreach {${clear_npcs}[%*]} {npc} {clear_npc ${npc}};
	#unaction {^    这里%*出口};
	#unaction {你想杀谁};
	#class hb_setup_inner open;
	#action {镖车呼啦一声散了架} {
		hb.pause;
		show_to_tuiche 镖车呼啦一声散了架;
	};

	#action {chaos.remember: done} {
		hb.move;
	};

	#action {^你赶着镖车驶了过来。$} {
		#math hb_path_index ${hb_path_index}+1;
		#list hb_path get ${hb_path_index} hb_next_step;
		#unticker {hb_walk};
		#if {${hb_paused} == 0} {
			#if {@getenv{mixin_lunci} == 5} {
				hb.move;
			};
			#else {
				chaos.remember;
			};
		};
	};

	#action {劫匪个起纵遁入暗里不见了} {
		hb.detect_enemy;
		#var hb_wait 0;
		raw;
	};

	#action {^这个方向} {
		#if {${hb_paused} == 0} {
			hb.pause;
			show_to_info 路径出错;
			xmpp.notify 路径出错;
			#alias {hb.recover} {
				#cr;
			};
		};
	};
	
	#action {劫匪趁你不注意，推着镖车就跑，你赶紧追了上去。} {
		hb.pause;
		@setenv{mixin_lunci;6};
		#delay {hb_delay} {
			hb.recover;
		} {3};
	};

	#action {劫匪伸手一拦道} {
		war;
		hb.wait;
	};

	#action {你长笑一声，开始对周围众人痛下杀手！} {
		#var hb_first_kill 1;
	};
	
	#action {劫匪突然从暗处跳了出来} {
		hb.detect_enemy;
		hb.wait;
		#if {${hb_first_kill} == 0} {
			killall robber;
		};
		#else {
			war;
		};
	};

	#action {围上来几个伙计将镖车拉了进去} {
		@setenv{hb_state;ENDED};
		hb.end;
		raw;
		show_to_tuiche 任务完成;
		notify escort done;
		hb.teardown;
		hb.back;			
	} {1};

	#class hb_setup_inner close;
	
	#alias ge {gan che to east};
	#alias genter {gan che to enter};
	#alias geu {gan che to eastup};
	#alias gn {gan che to north};
	#alias gne {gan che to northeast};
	#alias gnu {gan che to northup};
	#alias gnw {gan che to northwest};
	#alias gout {gan che to out};
	#alias gs {gan che to south};
	#alias gse {gan che to southeast};
	#alias gsu {gan che to southup};
	#alias gsw {gan che to southwest};
	#alias gw {gan che to west};
	#alias gwu {gan che to westup};
	#alias ged {gan che to eastdown};
	#alias gnd {gan che to northdown};
	#alias gwd {gan che to westdown};
	#alias gsd {gan che to southdown};
	#alias gd {gan che to down};
	#alias gu {gan che to up};

	#alias {attack} {
		attack.hubiao;
	};

	#var hb_elapsed_time 0;
	#ticker {hb_ticker} {
		#math hb_elapsed_time ${hb_elapsed_time}+1;
		show_to_tuiche ${hb_info} --> ${hb_elapsed_time}:00;
	} {60};
};

#alias {hb.wait} {
	#var hb_wait 1;
	#delay {hb_wait} {
		#var hb_wait 0;
	} {8};
};

#alias {hb.pause} {
	hb.end;
	#alias {hb.back} {
		#cr;
	};
};

#alias {hb.end} {
	#var hb_paused 1;
	#var hb_force_wait 1;
	#unticker {hb_walk};
	#undelay {hb_move};
};

#alias {hb.escape} {
	#if {${hb_path_index} > ${hb_path_size}} {
		on_truce {
			hb.resume;
		};
	};
	#else {
		#class hb.escape.inner open;
		#action {不能移动} {
			#showme 你逃跑失败;
		};
		
		#action {^你逃跑失败} {
			#class hb.escape.inner kill;
			on_truce {
				#delay {hb_delay} {
					hb.resume;
				} {3};
			};
		};
		
		#action {^    这里%*出口} {
			#class hb.escape.inner kill;
			#math hb_path_index ${hb_path_index}+1;
			#delay {hb_delay} {
				hb.resume;
			} {3}
		} {1};
		
		#delay {hb_delay} {
			#showme 你逃跑失败;				
		} {5};
		#class hb.escape.inner close;
		
		#list hb_path get ${hb_path_index} escape_exit;
		#3 halt;
		#delay {1} {
			${escape_exit};
		};
	};
};

#alias {hb.resume} {
	#var hb_paused 0;
	#var hb_force_wait 0;

	#alias {hb.back} {
		hb.merge_path;
		hb.escape;
	};

	#alias {hb.recover} {
		#class hb.recover.inner open;
		#action {chaos ok: %*} {
			#if {"%%%1" != "fake"} {
				#math hb_path_index ${hb_path_index}-1;
				#list hb_path set ${hb_path_index} {gan che to %%%1};
			};
			#nop chaos.remember;
			#delay {hb_delay} {
				hb.resume;
			} {10};
			#class hb.recover.inner kill;
		};

		#action {chaos error} {
			show_to_info 乱入;
			notify.alert escort chaos;
			xmpp.notify 乱入;
			#alias {hb.recover} {
				#cr;
			};
			#class hb.recover.inner kill;
		};
		#class hb.recover.inner close;		
		chaos.find;
	};

	#list hb_path get ${hb_path_index} hb_next_step;
	#if {"@getenv{hb_state}" == "STARTED"} {
		hb.move;
	};
	#else {
		#action {^    这里%*出口} {
			#math hb_path_index ${hb_path_index}+1;
			#list hb_path get ${hb_path_index} hb_next_step;
		} {1};
		#alias {l} {#cr};
		
		#ticker {hb_walk} {
			#if {${hb_path_index} > ${hb_path_size}} {
				#unaction {^    这里%*出口};
				#unalias {l};
				hb.pause;
				#delay {5} {
					#if {@getenv{hb_lunci} == @getenv{mixin_lunci}} {
						chain on_sell get.qian get.san repair special_repair gem.combine hb.b;
					};
					#else {
						hb.b;	
					};
				};
			};
			#else {
				${hb_next_step};
			};
		} {1.8};
	}
};

#alias {hb.get_owner} {
	#if {"${CurrentPosition}" == "zuo"} {
		#var hb_owner zuo;
		#var hb_rotate_location fm;
	};
	#elseif {"${CurrentPosition}" == "zz"} {
		#var hb_owner zhang;
	};
	#elseif {"${CurrentPosition}" == "fm"} {
		#var hb_owner ma;
		#var hb_rotate_location tf;
	};
	#elseif {"${CurrentPosition}" == "fh"} {
		#var hb_owner chu;
	};
	#elseif {"${CurrentPosition}" == "tf"} {
		#var hb_owner wang;
		#var hb_rotate_location zuo;
	};
};

#alias {hb.d} {
	hb.get_owner;
	#if {"@getenv{hb_state}" == "GOT_CAIWU"} {
		give cai wu to ${hb_owner};
	};
	#else {
		#if {@getenv{hb_lunci} == @getenv{mixin_lunci}} {
			@setenv{hb_lunci;0};
			notify.alert next mixin;
			xmpp.notify next mixin;
			play alert.wav;
		};
		#else {
			ask ${hb_owner} about finish;			
		};
	};
};

#alias {hb.q} {
	hb.get_owner;
	#if {"%1" != ""} {
		getesc %1;
	};
	#else {
		listesc;
	}
};


#alias {hb.f} {
	@setenv{hb_lunci;0};
	@setenv{hb_lunci_total;0};
	@setenv{hb_state;NIL};
	@setenv{mixin_lunci;6};
	hb.teardown;
	hb.get_owner;
	ask ${hb_owner} about fail;
};

#alias {hb.b} {
	#if {"${hb_owner}" == "zuo"} {
		gt zuo;
	};
	#elseif {"${hb_owner}" == "zhang"} {
		gt zz;
	};
	#elseif {"${hb_owner}" == "ma"} {
		gt fm;
	};
	#elseif {"${hb_owner}" == "wang"} {
		gt tf;
	};

	on_there {
		hb.d;
	};
};

#alias {hb.bxs} {
	#class hb.auto.inner open;
	#action {${char_name}把这批红货送到%*他已经派了个伙计名叫%*到%*附近接你} {
		hb.get_path %%1;
		show_to_quest 推车: ${tmp}|%%2|%%3;
		hb.setup;
		hb.resume;
	} {1};
	#class hb.auto.inner close;
	hb.get_owner;
	ask ${hb_owner} about 押镖;
};

#alias {hb.start} {
	set brief 2;
	#var hb_auto 1;
	#var getesc_current_score 0;
	#var getesc_current_id 0;

	#class hb.auto.inner open;
	#action {^你不是才要了任务吗} {
		hb.f;
		on_unbusy {
			hb.start;
		};
	};
	
	#action {你一共被奖励了：} {
		#class tmp.inner open;
		#action {%*点经验；} {
			#class tmp.inner kill;
			show_to_info @ctd{%%%1} 点经验;
		};
		#delay {hb_auto_delay} {
			#if {${hb_auto} == 1} {
				hb.start;
			};
		} {3};
		#class tmp.inner close;
	};

	#action {${char_name}帮%*追回财物，额外获得%*点经验} {
		show_to_info 额外获得 @ctd{%%2} 点经验;
	};

	#alias {got_gem} {
		#action {你将手中%*{◎|☆|★}%*放进了宝石袋} {
			show_to_info 掉宝: %%%1%%%2%%%3;
			notify.info %%%1%%%2%%%3;
			gem.record %%%1%%%2%%%3;
			#var gem_level @gemlevel{%%%1};
			#if {${gem_level} > 3} {
				play gem2.wav;
				xmpp.notify 掉宝: %%%1%%%2%%%3;
			};
			#else {
				play gem.wav;	
			};
		};
		pack gem;
		#delay {pack_gem} {
			#unaction {你将手中%*{◎|☆|★}%*放进了宝石袋};
		} {3};
	};
	
	#action {哇，你太厉害了，这么快就把这些财物追回，我有重谢！} {
		got_gem;
	};
	
	#action {掉了出来一颗} {
		got_gem;
	};
	
	#action {从劫匪身上掉了出来一%*之%*} {
		jianding @mapget{jianding;%%2}
	};

	#action {^你捧着%*之%*上上下下仔仔细细的打量了一遍。} {
		#action {可塑性:%*(%*)} {
			#unaction {可塑性:%*(%*)};
			#var kong %%%1;
			#var type @mapget{jianding;%%2};
			#if {"${type}" == "ring" || "${type}" == "necklace"} {
				#if {${kong} > 1} {
					put ${type} in bao;	
				};
			};
			#else {
				#if {${kong} > 2} {
					put ${type} in bao;	
				};
			};
		};
		#action {你将一%*之%*放进包袱} {
			#unaction {你将一%*之%*放进包袱};
			show_to_info 掉装备: ${kong} 孔 ${type};
			notify.alert 掉装备: ${kong} 孔 ${type};
			xmpp.notify 掉装备: ${kong} 孔 ${type};
		};
	};

	#action {看来该找机会逃跑了} {
		hb.pause;
		show_to_tuiche 丢镖;
		xmpp.notify 丢镖;
	};

	#action {^慢慢地你终于又有了知觉....} {
		show_to_tuiche 挂了;
		notify.alert 挂了; 
		xmpp.notify 挂了;
		hb.teardown;
		hb.back;			
	};
	
	#action {^你的眼前一黑，接著什么也不知道了} {
		hb.pause;
	};

	#action {使用命令【getesc 任务序号】来认领押镖任务。} {
		#buffer lock;
	};
	
	#action {%d%s%S%s%S%s待认领} {
		#var getesc_id %%1;
		#var getesc_address %%3;
		#var score @getesc_get_score{${getesc_address}};
		#if {${score} > ${getesc_current_score}} {
			#var getesc_current_score $score;
			#var getesc_current_id ${getesc_id};
		};
	};

	#action {来认领押镖任务} {
		#if {${getesc_current_id} != 0} {
			#var hb_rotate_location_count 0;
			hb.q ${getesc_current_id};
		};
		#else {
			#math hb_rotate_location_count ${hb_rotate_location_count} + 1;
			#if {${hb_rotate_location_count} > 3 && "${hb_rotate_location}" != "NIL"} {
				#var hb_rotate_location_count 0;
				hb.stop;
				gt ${hb_rotate_location};
				on_there {
					hb.start;
				};
			};
			#else {
				#delay {hb_auto_delay} {
					hb.start;
				} {10};
			};
		};
	};

	#action {请选择其他任务} {
		#delay {hb_auto_delay} {
			#var getesc_current_score 0;
			#var getesc_current_id 0;
			hb.q;
		} {2};
	};

	#action {你上次运镖太辛苦了} {
		#delay {hb_auto_delay} {
			hb.q;
		} {10};
	};

	#action {你所领的任务在任务列表中并不存在} {
		#delay {hb_auto_delay} {
			hb.q;
		} {2};
	};
	
	#action {${char_name}把这批红货送到%*他已经派了个伙计名叫%*到%*附近接你} {
		hb.get_path %%1;

		@setenv{hb_state;STARTED};
		@setenv{hb_lunci;@eval{@getenv{hb_lunci}+1}};

		#var hb_info ${tmp}|%%2|%%3;
		show_to_tuiche ${hb_info};
		pp %%2;
		notify escort start;

		hb.setup;
		hb.resume;
	} {1};

	#action {递给你一封密信} {
		@setenv{hb_state;GOT_MIXIN};
		@setenv{hb_lunci_total;@eval{@getenv{hb_lunci_total}+1}};
		@setenv{hb_lunci;0};
		show_to_tuiche 密信;
		notify.alert 密信;
		xmpp.notify 密信;
		play alert.wav;
		chifan;
		mx.o;
	};

	#class hb.auto.inner close;

	@declarenv{hb_state;NIL};
	@declarenv{mixin_lunci;6};
	
	#if {"@getenv{hb_state}" == "GOT_CAIWU"} {
		show_to_info 交财物先;
	};
	#if {"@getenv{hb_state}" == "GOT_MIXIN"} {
		show_to_info 密信;
	};
	#elseif {"@getenv{hb_state}" == "STARTED"} {
		hb.f;
	};
	#else {
		#math next_lunci @getenv{hb_lunci}+1;
		show_to_quest 推车: @getenv{hb_lunci_total}+${next_lunci}: 准备;
		on_health {hb.q};		
	};
};


#alias {hb.stop} {
	#var hb_auto 0;
	#class hb.auto.inner kill;
	#undelay {hb_auto_delay};
	dz.cancel;
};

#alias {hb.merge_path} {
	@stackcreate{tmp};
	@stackpush{tmp;#cr};

	#while {${hb_path_index} <= ${hb_path_size}} {
		#list hb_path get ${hb_path_index} v;
		#list tmp_path get ${tmp_index} tmp_top;
		#if {"@opposite{${v}}" == "@stacktop{tmp}"} {
			#var nil @stackpop{tmp};
		};
		#elseif {"@opposite{${v}}" != "NULL"} {
			@stackpush{tmp;${v}};			
		}
		#math hb_path_index ${hb_path_index}+1;
	}
	#var hb_path @stacktolist{tmp};
	#var hb_path_index 2;
	#list hb_path size hb_path_size;
};


#alias {hb.guohe} {
	#action {^%*接过你递给的船资} {
		#class guohe.inner open;
		#ticker {sw/yell_boat} {
			yell boat;
		} {5};
		#action {正等着你呢，上来吧。} {
			genter;
		};
		#action {艄公将一块踏脚板搭上堤岸，} {
			genter;
		};
		#alias {shangchuan} {
			#unticker {sw/yell_boat};
			#unticker {hb_walk};
			#action {所有人被赶下了渡船} {
				#unaction {所有人被赶下了渡船};
				#class guohe.inner kill;
				hb.resume;
			};
		};
		#action {^黄河渡船$} {
			shangchuan;
		};
		#action {^长江渡船$} {
			shangchuan;
		};
		#class guohe.inner close;
	};
	#var hb_paused 1;
	ask shao gong about %1;		
};

#alias {clear_blocker} {
	#var tmp_blocker %1;
	#class clear_blocker.inner open;
	#action {你想杀谁} {
		#class clear_blocker.inner kill;
		#math hb_path_index ${hb_path_index}+1;
		hb.resume;
	};
	#ticker {tmp_blocker_ticker_kill} {
		killall ${tmp_blocker};	
	} {5};
	#class clear_blocker.inner close;
	hb.pause;
	killall ${tmp_blocker};
};

#alias {clear_npc} {
	#var npc_zh %1;
	#class clear_npc.inner open;
	#action {你想杀谁} {
		#var hb_force_wait 0;
	};
	#action {${npc_zh}死了} {
		#var hb_force_wait 0;
	} {1};

	#action {你还是先把对手解决了再说吧} {
		l;
	} {1};
	
	#action {${npc_zh}(%*)} {
		#action {看起来${npc_zh}想杀死你} {
			#var hb_force_wait 1;
		} {1};
		#var npc_en @lower{%%1};
		killall ${npc_en};
    } {1};
	#class clear_npc.inner close;
};

#alias {mx.o} {
	xian mixin;
};

#alias {mx.re} {
	hb.get_owner;
	give mixin to ${hb_owner};
	mx.o;
};

#alias {mx.q} {
	#class mx.inner open;	
	#action {一个伙计挖着鼻屎走了出来，道：你找我啥事} {
		l;
	};
	#action {财物的伙计 %S(%S)} {
		#format {tmp_huoji} {%l} {%%2};
		#class mx.yao.inner open;
		#action {把一包财物砸向你，一转眼不见了。} {
			@setenv{hb_state;GOT_CAIWU};
			show_to_tuiche 拿到财宝;
			play get_item.wav;
			#class mx.yao.inner kill;
			#class mx.inner kill;
			back;
		};
		#action {这里没有这个人} {
			#class mx.yao.inner kill;	
		};
		#ticker {yao} {
			ask $tmp_huoji about yao;
		} {0.5};
		#class mx.yao.inner close;
	} {1};
	#class mx.inner close;
	zhao;
};

#alias {mx.d} {
	hb.get_owner;
	give cai wu to ${hb_owner};
	#class mx.inner kill;
};

#alias {hb.p} {
	chain get.san get.qian get.fan get.bao repair;
};

#action {你给%*一些失踪的财物} {
	@setenv{hb_state;NIL};
	@setenv{mixin_lunci;5};
	#delay {hb_auto_delay} {
		#if {${hb_auto} == 1} {
			hb.start;
		};
	} {3};
};

#alias {show_to_tuiche} {
	show_to_quest 推车: @getenv{hb_lunci_total}+@getenv{hb_lunci}: %0;
};

#alias {got_robber} {
	#delay {detect_enemy_delay} {
		#class detect_enemy.inner kill;	
	} {1};
	#math robber_count ${robber_count}+1;
	#var tmp_score @mapget{robber_score;%1};
	#if {${tmp_score} > ${robber_score}} {
		#var robber_score ${tmp_score};
		#var enemy ${char_id}'s robber ${robber_count};
	};
};

#alias {hb.detect_enemy} {
	#class detect_enemy.inner open;
	#action {~[1;31m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber quanzhen;
	};
	#action {~[1;32m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber shaolin;
	};
	#action {~[1;33m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber gaibang;
	};
	#action {~[1;34m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber xingxiu;
	};
	#action {~[1;35m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber huashan;
	};
	#action {~[1;36m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber taohua;
	};
	#action {~[1;37m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
		got_robber wudang;		
	};
	#action {风景要慢慢的看} {
		#delay {0.5} {
			l;
		};
	};
	#class detect_enemy.inner close;
	#undelay {detect_enemy_delay};
	#var enemy NULL;
	#var robber_count 0;
	#var robber_score 0;
	l;
};
#class hubiao close
