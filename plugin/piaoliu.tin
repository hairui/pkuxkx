#class piaoliu open
#var pl_npc_c nil;
#alias {pl.q} {
	ask ren about job;
};

#alias {pl.start} {
	#if {"%2" == "n"} {
		#var hua_direction north;
	};
	#elseif {"%2" == "s"} {
		#var hua_direction south;
	};
	#elseif {"%2" == "e"} {
		#var hua_direction east;
	};
	#elseif {"%2" == "w"} {
		#var hua_direction west;
	};
	#var hua_count %3;
	#var pl_count %1;
	show_to_quest 漂流: ${pl_count} 里后, 往 ${hua_direction} 划 ${hua_count} 里;
	shang boat;
	jie sheng;

	#class piaoliu.inner open;
	#action {小船被你向%*划出了%*里了} {
		#math hua_count ${hua_count}-1;
		show_to_quest 漂流: ${pl_count} 里后, 往 ${hua_direction} 划 ${hua_count} 里;
		#if {${hua_count} > 0} {
			hua ${hua_direction};
		};
		#else {
			xiamao;
			#showme 你决定再接再厉;
		};
	};

	#action {你决定再接再厉} {
		#delay {5} {
			dalao;
		};
	};

	#action {你仔细回忆了之前和鄱阳水贼战斗的经过} {
		raw;
	};

	#action {你总共击退了%*名水贼} {
		mj.stop;
		raw;
		#if {${pl_count} == 0} {
			#delay {2} {
				hua ${hua_direction};
			};
		};
	};

	#action {现在不能划桨} {
		#delay {2} {
			hua ${hua_direction};
		};
	};

	#action {也许你可以返航了} {
		pl.d;
	};

	#action {小船已经顺流漂出了%*里开外了} {
		#math pl_count ${pl_count}-1;
		show_to_quest 漂流: ${pl_count} 里后, 往 ${hua_direction} 划 ${hua_count} 里;
		#if {"%%1" == "十"} {
			sheng;
		};
		#if {${pl_count} == 0} {
			sheng;
			hua ${hua_direction};
		};
	};

	#action {一个鄱阳水贼爬上了你的小船} {	
	    killall;
	};

	#action  {^%*突然神秘的一笑，双眼紧叮着你，象要把你的魂摄下来。} {
		#var pl_npc_c %%1;
		show_to_info 古墓:${pl_npc_c};
		step;
		check_shuizei;
	};

	#action {mingjiao enemy:%*} {
		#var pl_npc_c %%1;
		show_to_info 明教:${pl_npc_c};
		step;
		check_shuizei;
	};

	#action {你想用机关对付谁} {
		#unticker {zhi};	
	};
	
	#action {机关已经使用殆尽} {
		#unticker {zhi};
	};
	
	#action {机关刚刚完全运转} {
		#unticker {zhi};
	};
	
	#action {^机关继续推动，把%*挤下水去。} {
		#unticker {zhi};
		#if {${pl_count} == 0} {
			#delay {2} {
				hua ${hua_direction};
			};
		};
	};
	
	#class piaoliu.inner close;
};

#alias {pl.d} {
	hua back;
	give ren xiang;
	#class piaoliu.inner kill;
};

#alias {pl.f} {
	ask ren about fail;
	#class piaoliu.inner kill;
};

#alias {check_shuizei} {
	#delay {2} {
		#send {l};
	};
	#action {水贼 %S(%S %S)} {	
	      #if {"${pl_npc_c}" == "%%1"} {
			  show_to_info zhi @lower{%%2};
			  #ticker {zhi} {
				  zhi @lower{%%2};
			  } {2};
		  };
	};
	#delay {5} {
		#unaction {水贼 %S(%S %S)};
	};
};
#class piaoliu close