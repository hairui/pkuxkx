#class chaos open
#var position "";

#gag get_position: done;
#alias {get_position} {
	#var record_state 0;
	#var position {};
	#var action %0;
	#class record.inner open;
	#action {^%S - $} {
		#var record_state 1;
		#var current_room %%1;
	} {1};

	#action {^    %*} {
		#if {$record_state == 1} {
			#var description %%1;
			#var record_state 2;
		};
	} {9};

	#action {^    这里%*的出口是%*} {
		#var description ${description}%%0;
		#var position "${current_room}@@${description}";
		#class record.inner kill;
		#showme get_position: done;
	} {8};
	
	#action {^%S%*} {
		#if {$record_state == 2} {
			#var description ${description}%%1%%2
		}
	} {9};
	
	#ticker {l} {
		${action};
	} {1.5};
	#class record.inner close;
	${action};
};

#gag chaos.remember: done;
#alias {chaos.remember} {
	#action {get_position: done} {
		#unaction {get_position: done};
		#var last_position ${position};
		#showme chaos.remember: done;
	};
	get_position {l};
};

#gag {chaos};
#alias {chaos.find} {
	#action {get_position: done} {
		#if {${last_position} == ${position}} {
			#showme chaos ok: ${exit};
			#unaction {get_position: done};
		};
		#else {
			#math exits_list_index ${exits_list_index}+1;
			#delay {1} {
				check;
			};
		};	
	};
	#alias {check} {
		#if {${exits_list_index} <= ${exits_list_size}} {
			#list exits_list get ${exits_list_index} exit;
			#if {"${exit}" == "fake"} {
				get_position {l};								
			};
			#else {
				get_position {l ${exit}};				
			};
		};
		#else {
			#showme chaos error;
			#unaction {get_position: done};
		};
	};
	#action {get_exits: done} {
		#unaction {get_exits: done};
		#var exits_list_index 1;
		#list exits_list size exits_list_size;
		#delay {1} {
			check;
		};
	};
	get_exits;
};

#list exits_list create {};
#gag {get_exits: done};
#alias {get_exits}  {
	#class get_exits.inner open;
	#alias {get_exits_done} {
		#class get_exits.inner kill;
		#list exits_list add {fake};
		#showme {get_exits: done};
	};
	#action {这里唯一的出口是 %w。} {
		#list exits_list create {%%1};
		get_exits_done;
	};

	#action {这里明显的出口是 %w 和 %w。} {
		#list exits_list create {%%1;%%2};
		get_exits_done;
	};

	#action {这里明显的出口是 %w、%w 和 %w。} {
		#list exits_list create {%%1;%%2;%%3};
		get_exits_done;
	};

	#action {这里明显的出口是 %w、%w、%w 和 %w。} {
		#list exits_list create {%%1;%%2;%%3;%%4};
		get_exits_done;
	};

	#action {这里明显的出口是 %w、%w、%w、%w 和 %w。} {
		#list exits_list create {%%1;%%2;%%3;%%4;%%5};
		get_exits_done;
	};

	#action {这里明显的出口是 %w、%w、%w、%w、%w 和 %w。} {
		#list exits_list create {%%1;%%2;%%3;%%4;%%5;%%6};
		get_exits_done;
	};

	#action {这里明显的出口是 %w、%w、%w、%w、%w、%w 和 %w。} {
		#list exits_list create {%%1;%%2;%%3;%%4;%%5;%%6;%%7};
		get_exits_done;
	};

	#ticker {look} {
		#send {l};
	} {1};
	#class get_exits.inner close;
	#send {l};
};

#class chaos close