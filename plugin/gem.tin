#class gem open
#var combine_target 7;
#list gem_types create {地;山;水;风;雷;火;泽;天};
#var gem.combine_count 0;
#alias {gem} {
	jiancha jin nang;
	lock 用pack <物品id>将宝石放入其中;
};

#function {gemlevel} {
	#list gem_types find %1 result;
};

#alias {check_gem} {
	#var gem_id %1;
	#var gem_type %2;
	#var gem_count %3;
	#list gem_types find ${gem_type} gem_type_index;
	#if {${gem_count} >=3 && ${gem_type_index} < ${combine_target}} {
		#var target_gem_id ${gem_id};
	};
	
};

#alias {gem.combine} {
	#alias tmp_combine %1;
	#class gem.combine.inner open;
	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		check_gem {%%2} {%%3} {%%6};
		check_gem {%%9} {%%10} {%%13};
		check_gem {%%16} {%%17} {%%20};
		check_gem {%%23} {%%24} {%%27};
		check_gem {%%30} {%%31} {%%34};
	} {1};

	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		check_gem {%%2} {%%3} {%%6};
		check_gem {%%9} {%%10} {%%13};
		check_gem {%%16} {%%17} {%%20};
		check_gem {%%23} {%%24} {%%27};
	} {2};

	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		check_gem {%%2} {%%3} {%%6};
		check_gem {%%9} {%%10} {%%13};
		check_gem {%%16} {%%17} {%%20};
	} {3};

	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		check_gem {%%2} {%%3} {%%6};
		check_gem {%%9} {%%10} {%%13};
	} {4};

	#action {(%s%S)%*{◎|☆|★}%*\*%S} {
		check_gem {%%2} {%%3} {%%6};
	} {5};

	#action {你的宝石袋里空空荡荡} {
		combine_gems_done;
	};

	#action {用pack <物品id>将宝石放入其中} {
		combine_gems;
	};

	#alias {combine_gems_done} {
		#class gem.combine.inner kill;
		#show 合成完毕;
		#delay {3} {
			tmp_combine;
		};
	};

	#alias {combine_gems_prepare} {
		#var target_gem_id -1;
		#3 {pack gem};
		#var gem.combine_count 0;
		jiancha jin nang;
	};

	#alias {combine_gems} {
		#if {${target_gem_id} != -1} {
			#3 {pickgem ${target_gem_id}};
			#delay {3} {
				combine gem;
			};
			#delay {4} {
				pack gem;
			};
			#if {${nl_percent} > 20} {
				#delay {5} {
					combine_gems_prepare;
				};
			};
			#else {
				combine_gems_done;
			};
		};
		#else {
			combine_gems_done;
		};
	};
	#class gem.combine.inner close;

	combine_gems_prepare;
};

#alias {gem.transfer} {
	#var receiver %1;
	#class gem.transfer.inner open;
	#action {你的宝石袋里空空荡荡} {
		#class gem.transfer.inner kill;
	};
	#action {你给%*一颗} {
		pickgem 1;
	};
	#action {你从宝石袋里面取出} {
		#delay {1} {
			give ${receiver} gem;
		};
	};
	#action {这里没有 ${receiver}} {
		show_to_info 附近没有 ${receiver};
		#class gem.transfer.inner kill;
		pack gem;
	};
	#class gem.transfer.inner close;
	pickgem 1;
};

#alias {mark_gem} {
	#var gem_id %1;
	#var gem_type %2;
	#var gem_yinyang %3;
	#var gem_name %4;
	#list gem_types find ${gem_type} gem_type_index;
	#if {${gem_type_index} <= ${combine_target}} {
		#if {"${gem_name}" == "神龙骨" || "${gem_name}" == "凤凰羽" || "${gem_name}" == "玄武甲" || "${gem_name}" == "麒麟角" } {
			#var key ${gem_type}${gem_yinyang}${gem_name};
			@mapset{gem_mark;$key;${gem_id}};
			#if {"${gem_yinyang}" == "☆"} {
				#var rev_gem_yinyang ★;
			};
			#elseif {"${gem_yinyang}" == "★"} {
				#var rev_gem_yinyang ☆;
			};
			#else {
				#var rev_gem_yinyang n;
			};
			#var rev_key ${gem_type}${rev_gem_yinyang}${gem_name};
			#var rev_gem_id @mapget{gem_mark;${rev_key}};
			#if {"${rev_gem_id}" != "NULL"} {
				#if {${gem_id} > ${rev_gem_id}} {
					#var gem_id_1 ${gem_id};
					#var gem_id_2 ${rev_gem_id};
				};
				#else {
					#var gem_id_2 ${gem_id};
					#var gem_id_1 ${rev_gem_id};
				};
			};
		};
	};
};

#alias {gem.merge} {
	#var gem_id_1 -1;
	#var gem_id_2 -1;
	#var nil @mapcreate(gem_mark);

	#3 {pack gem};
	jiancha jin nang;

	#class gem.merge.inner open;
	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		mark_gem {%%2} {%%3} {%%4} {%%5};
		mark_gem {%%9} {%%10} {%%11} {%%12};
		mark_gem {%%16} {%%17} {%%18} {%%19};
		mark_gem {%%23} {%%24} {%%25} {%%26};
		mark_gem {%%30} {%%31} {%%32} {%%33};
	} {1};

	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		mark_gem {%%2} {%%3} {%%4} {%%5};
		mark_gem {%%9} {%%10} {%%11} {%%12};
		mark_gem {%%16} {%%17} {%%18} {%%19};
		mark_gem {%%23} {%%24} {%%25} {%%26};
	} {2};

	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		mark_gem {%%2} {%%3} {%%4} {%%5};
		mark_gem {%%9} {%%10} {%%11} {%%12};
		mark_gem {%%16} {%%17} {%%18} {%%19};
	} {3};

	#action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
		mark_gem {%%2} {%%3} {%%4} {%%5};
		mark_gem {%%9} {%%10} {%%11} {%%12};
	} {4};

	#action {(%s%S)%*{◎|☆|★}%*\*%S} {
		mark_gem {%%2} {%%3} {%%4} {%%5};
	} {5};
	#class gem.merge.inner close;

	#alias {merge_gems} {
		#if {${gem_id_1} != -1} {
			pickgem ${gem_id_1};
			pickgem ${gem_id_2};
			#delay {3} {
				merge gem;
				#delay {3} {
					pack gem;
					#if {${nl_percent} > 20} {
						#delay {5} {
							gem.merge;
						};
					};
					#else {
						show_to_info 内力不足, 无法 merge gem;
					};
				};
			};
		};
		#else {
			show_to_info 无法合成;
		};
	};
	#action {用pack <物品id>将宝石放入其中} {
		#unaction {用pack <物品id>将宝石放入其中};
		#class gem.merge.inner kill;
		merge_gems;
	};
};

#alias {gem.record} {
	#var gem_name %1;
	#format {current_epoch} {%T};
	#format {current_time} {%t} {{%T}{$epoch}};
	#system {sqlite3 gem_record.db 'insert into gem_record values (NULL, "${char_id}", "${current_time}", ${current_epoch}, "${gem_name}")'};
};

#alias {gem.query} {
	#format {current_epoch} {%T};
	#math last_epoch ${current_epoch} - 24*60*60;
	#system {sqlite3 gem_record.db 'select time, gem from gem_record where epoch > ${last_epoch} and char_id = "${char_id}" order by epoch'};
};

#class gem close