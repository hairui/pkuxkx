#class wudang_spec open
#var jiali_count max;

#alias {attack.prepare} {
    yun powerup;
    #class attack.inner open;
    #delay {7.8} {
        pfm zong;
    };
    #ticker {perf_chan} {
        pfm chan;
    } {0.6};
    #class attack.inner close;
};

#alias {attack} {
    pfm chan;
    #if {"${quest}" == "hb"} {
        #class attack.inner open;
        #class attack.inner close;
    };
    #elseif {${mj_started}  == 1} {
        jifa_none;
        #class attack.inner open;
        #ticker {perf_zhen} {
            pfm zhen;
        } {1};
        #class attack.inner close;
        #if {${xf.type} == -1 || ${xf.type} == 1} {
            pfm wuying;
            pfm gangrou;
            #class attack.inner open;
            #ticker {wuying} {
                pfm wuying;
            } {5.8};
            #ticker {gangrou} {
                pfm gangrou;
            } {6.8};
            #class attack.inner close;
        };
        #elseif {${xf.type} == 2} {
            pfm wuying;
            #class attack.inner open;
            #ticker {wuying} {
                pfm wuying;
            } {5.8};
            #class attack.inner close;
        };
        #elseif {${xf.type} == 4} {
            pfm wuying;
            #class attack.inner open;
            #class attack.inner close;
        };
        #else {
            #class attack.inner open;
            #class attack.inner close;
        };
    };
    #else {
        #if {${xf.type} == -1 || ${xf.type} == 1} {
            pfm lzj;
            pfm wuying;
            pfm gangrou;
            #class attack.inner open;
            #ticker {lzj} {
                pfm lzj;
            } {8.8};
            #ticker {wuying} {
                pfm wuying;
            } {6.8};
            #ticker {gangrou} {
                pfm gangrou;
            } {5.8};
            #class attack.inner close;
        };
        #elseif {${xf.type} == 2} {
            jifa_none;
            pfm wuying;
            #class attack.inner open;
            #ticker {wuying} {
                pfm wuying;
            } {5.8};
            #class attack.inner close;
        };
        #elseif {${xf.type} == 4} {
            pfm wuying;
            #class attack.inner open;
            #class attack.inner close;
        };
        #else {
            jifa_none;
            #class attack.inner open;
            #class attack.inner close;
        };
    };
};

#var shortcut F1:缠 F2:无影 F3:连字诀 F4:刚柔;
#macro {\eOQ} {
    pfm_oob wuying;
};

#macro {\eOR} {
    pfm_oob lzj;
};

#macro {\eOP} {
    pfm_oob chan;
};

#macro {\eOS} {
    pfm_oob gangrou;
};

#alias {chan} {
    unwield all;
    wield @firstweapon{};
    #2 perform taiji-jian.chan;
    wi;
};

#alias {sanhuan} {
    unwield all;
    wield @firstweapon{};
    jiali ${jiali_count};
    perform taiji-jian.sanhuan;
    #if {${gangrou_ing} == 0} {
        jiali none;
    };
    wi;
};

#alias {zhen} {
    unwi;
    perform taiji-quan.zhen;
    wi;
};

#alias {zong} {
    perform tiyunzong.zong;
};

#alias {lzj} {
    unwi;
    wield @firstweapon{};
    jiali 10;
    perform taiji-jian.lian;
    #if {${gangrou_ing} == 0} {
        jiali none;
    };
    wi;
};

#alias {szj} {
    unwi;
    wield @firstweapon{};
    perform taiji-jian.sui;
    wi;
};

#alias {wuying} {
    unwield all;
    jiali ${jiali_count};
    perform claw.wuying;
    #if {${gangrou_ing} == 0} {
        jiali none;
    };
    wi;
};

#var gangrou_ing 0;
#alias {gangrou} {
    #var gangrou_ing 1;
    unwi;
    jiali ${jiali_count};
    perform taiji-quan.gangrou;
    #action {抬手就封住了你这一掌！} {
        #unaction {抬手就封住了你这一掌！};
        #ticker {perf_zhen_2} {
            pfm zhen;
        } {1};
        #delay {delay_gangrou} {
            #unticker {perf_zhen_2};
            #var gangrou_ing 0;
            jiali none;
        } {5};
    };
    #delay {delay_gangrou} {
        #unaction {抬手就封住了你这一掌！};
    };
    wi;
};

#alias {lianhuan} {
    perform spear.lianhuan;
};

#alias {map_goto_lingwu} {
    map.go {静修阁};
};

#alias {map_goto_rest} {
    map.go {客房};
};

#alias {map_goto_start} {
    map.go {武当广场};
};

#alias {map_goto_shifu} {
    map.go {小屋};
};

#list my_weapons create {my sword;};
#list my_weapons_1 create {sword; sword 2};

#var shifu zhang;

#alias {wi_xiaofeng} {
    unwi;
    wield sword;
};

#alias {reborn} {
    #send {jifa cuff taiji-quan};
    #send {jifa dodge tiyunzong};
    #send {jifa parry taiji-quan};
    #send {jifa sword taiji-jian};
    #send {jifa blade xuanxu-dao};
    #send {jifa claw taiji-zhao};
    #send {jifa force taiji-shengong};
    bei cuff claw;
    wbei taiji-jian xuanxu-dao;
};
#var char_menpai_position wd;

stat.register 缠 miss 只听得「铮」一声清响，剧烈碰撞之下你剑诀为敌所破，无以为继，被迫撤剑收招。;
stat.register 缠 hit 只能见招拆招，疲于应对，不觉已深陷太极剑意之中，无法出招。;

#var menzhong zhang;
#var menzhong_didian ht;

#alias {jifa_none} {
    jifa sword none;
};

#alias {jifa_all} {
    jifa sword taiji-jian;
};
#class wudang_spec close
