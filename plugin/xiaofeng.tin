#class xiaofeng open
#var xf.type -1;
#alias {xf.teardown} {
    #var quest NIL;
    #var xf.type -1;
    #var xf.requested 0;
    #class xiaofeng.inner kill;
};

#alias {xf.q} {
    #class xiaofeng.inner open;
    #var xf.requested 0;
    #action {跳出战圈不打了} {
        on_unbusy {
            jifa_all;
        };
        #if {${xf.type} == 3} {
            #delay {xf_delay} {
                quan;
            } {0.3};
        };
    };
    #action {^萧峰(xiao feng)告诉你：你先下去休息休息吧。} {
        #delay {xf_delay} {
            xf.q;
        } {10};
    } {1};
    #action {^萧峰说道：%*刚刚已经有人前去了} {
        #delay {xf_delay} {
            xf.q;
        } {3};
    };
    #action {^从%*身上掉了出来一颗%*} {
        pack gem;
        show_to_info 掉宝: %%2;
    };

    #action {^由于放弃任务} {
        show_to_quest 萧峰: 失败;
        xf.teardown;
    };

    #action {^你已经完成了} {
        show_to_quest 萧峰: 完成;
        xf.teardown;
    };

    #action {%*向萧峰打听有关『job』的消息} {
        #if {"%%1" == "你"} {
            #var xf.requested 1;
            #var quest xf;
        };
        #else {
            #var xf.requested 0;
        };
    };

    #action {传闻西夏一品堂派出了若干蒙面杀手，最近出现在%*附近的%*} {
        #if {${xf.requested} == 1} {
            #var xf.target %%1:%%2;
            #var target_area @area{%%1};
            #if {"${target_area}" != "NULL"} {
                gt ${target_area};
                on_there {
                    #if {@blsupport{${target_area} == 1}} {
                        xf.bl;
                    };
                };
            };
        };
    };

    #action {你去将他擒回这里交给我} {
        #if {${xf.requested} == 1} {
            #var xf.type 2;
            show_to_quest 萧峰: 擒回: ${xf.target};
        };
    };

    #action {你去将他除掉，取其首级回来。} {
        #if {${xf.requested} == 1} {
            #var xf.type 1;
            show_to_quest 萧峰: 杀死: ${xf.target};
        };
    };

    #action {『蒙面杀手(%*气血:%*\%/%*\%)』} {
        #if {%%2 < 50 || %%3 < 50} {
            xf.slow_down_1;
        };
        #if {%%2 < 35 || %%3 < 35} {
            xf.slow_down_2;
        };
    } {1};

    #action {看来杀手有点怕你了} {
        raw;
        on_unbusy {
            on_halt {
                quan;
            };
        };
    };

    #alias {xf.slow_down_1} {
        #if {${xf.type} == 4} {
            ask shashou about 认输;
        };
    };

    #action {蒙面杀手说道：「我不服，等我休息一下，咱们再打过！」} {
        raw;
        on_halt;
    };

    #action {蒙面杀手说道：「我还没恢复好，你想乘人之危么？」} {
        raw;
        unwi;
    };

    #alias {xf.slow_down_2} {
        #if {${xf.type} == 3 || ${xf.type} == 4} {
            #showme 快死了, 停手~;
            jifa_none;
            raw;
            unwi;
            on_halt;
        };
    };

    #action {尚可教化} {
        #if {${xf.requested} == 1} {
            #var xf.type 3;
            show_to_quest 萧峰: 劝: ${xf.target};
        };
    };

    #action {让他认输就好} {
        #if {${xf.requested} == 1} {
            #var xf.type 4;
            show_to_quest 萧峰: 战胜: ${xf.target};
        };
    };

    #action {你将%*扶了起来背在背上} {
        #showme 收工;
        show_to_quest 萧峰: 已擒获 ${xf.real_name};
        #action {${xf.real_name}略一凝神，精神看起来好多了} {
            #unaction {${xf.real_name}略一凝神，精神看起来好多了};
            dian ${xf.real_first_name};
            get ${xf.real_first_name};
        };
    };

    #action {西夏一品堂 %*(%* %*)} {
        #unaction {西夏一品堂 %*(%* %*)};
        #var xf.real_name %%1;
        #nop pp %%1;
        #var xf.real_first_name @lower{%%2};

        #if {${xf.type} == 2} {
            show_to_quest 萧峰: 擒回: ${xf.real_name};
            on_unbusy {
                get ${xf.real_first_name};
            };
        };
        #if {${xf.type} == 4} {
            show_to_quest 萧峰: 战胜: ${xf.real_name};
        };
    } {2};

    #action {西夏一品堂 蒙面杀手(Mengmian shashou)} {
        #if {${xf.type} != -1} {
            xf.check;
        };
    } {1};

    #action {要打便打} {
        #class xf.bl.inner kill;
        #if {${xf.type} != -1} {
            follow shashou;
        };
    };

    #action {你捡起一颗%*的首级} {
        #showme 收工;
        show_to_quest 萧峰: 已杀死;
    };

    #action {硬生生的将%*的头颅从尸体上扯了下来，提在手中！} {
        #showme 收工;
        show_to_quest 萧峰: 已杀死;
    };
    
    #action {将血淋淋的首级提在手中} {
        #showme 收工;
        show_to_quest 萧峰: 已杀死;
    };

    #action {血淋淋的首级咕噜噜的滚落在地} {
        on_unbusy {
            get shouji;
        };
    };

    #action {显是蒙面杀手姓名} {
        raw;
        jifa_all;
        #if {${xf.type} == 1} {
            #delay {1} {
                on_unbusy {
                    wi;
                    zhan corpse;
                };
            };
        };
        l;
    } {1};

    #action {蒙面杀手深深地叹了口气} {
        #unaction {『蒙面杀手(%*气血:%*\%/%*\%)』};
        #undelay {xf_delay};
        raw;
        jifa_all;
        on_halt;
        #if {${xf.type} == 3} {
            #showme 收工;
            show_to_quest 萧峰: 已经劝服;
        };
        #elseif {${xf.type} == 4} {
            #showme 收工;
            show_to_quest 萧峰: 已经战胜;
        };
    };

    #alias {xf.n} {
        detect_mingjiao shashou;
        #if {${xf.type} == 1} {
            kill shashou;
        };
        #elseif {${xf.type} == 2} {
            hit shashou;
        };
        #elseif {${xf.type} == 3} {
            hit shashou;
        };
        #elseif {${xf.type} == 4} {
            fight shashou;
        };
    };

    #alias {xf.check} {
        ask shashou about fight;
    };

    #class xiaofeng.inner close;

    ask xiao feng about job;
};

#alias {xf.d} {
    #class xf.bl.inner kill;
    #if {${xf.type} == 1} {
        give shouji to xiao;
    };
    #if {${xf.type} == 2} {
        give ${xf.real_first_name} to xiao;
    };
    #if {${xf.type} == 3} {
        ask xiao feng about 完成;
    };
    #if {${xf.type} == 4} {
        ask xiao feng about 完成;
    };
};

#alias {xf.f} {
    ask xiao feng about fail;
};

#alias {xf.bl} {
    #class xf.bl.inner open;
    #action {蒙面杀手说道：「就你也配跟我讨教功夫？！」} {
        #delay {xf.bl} {
            bl.continue {杀手};
        } {1};
    };
    #action {找到目标} {
        xf.check;
    };
    #class xf.bl.inner close;
    bl.start 蒙面杀手;
};

#alias {xf.b} {
    gt xf;
    on_there {
        xf.d;
    };
};

#alias {xf.heal} {
    yun lifeheal shashou;
    l shashou;
};
#class xiaofeng close